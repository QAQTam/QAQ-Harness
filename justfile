# QAQ-Harness Monorepo — 后端统一构建系统
# 用法: just [recipe]
#
# 项目结构:
#   crates/          Rust 后端 (18 crates)
#   dsh-minimal-mode/ 极简模式工具（bash_v2 + str_replace_editor）
#
# 说明：Windows 桌面层（WinUI3 壳 / installer / updater）已拆分为独立仓库
# F:\qaqh-winui-app；本仓库只保留跨平台后端核心与公共 SDK。
# 前端打包用 ../qaqh-winui-app/justfile。

set windows-shell := ["pwsh.exe", "-NoLogo", "-Command"]

# ── 默认 ────────────────────────────────────────────
default:
    @just --list

# ── 构建 ────────────────────────────────────────────

# 编译 daemon（后端核心，release）
build-daemon:
    cargo build --release -p qaqh-daemon -p qaqh-workspace

# ── 开发 ────────────────────────────────────────────

# 启动 daemon（dev profile）
dev:
    cargo run -p qaqh-daemon -- run

# ── 局域网 Web ───────────────────────────────────────

# 一键启动局域网 Web 服务（Windows）：构建检查 + daemon server 模式 +
# apps/web 产物托管，自动打印局域网访问地址与 token
[windows]
daemon-web port="64413":
    pwsh -NoProfile -File scripts/daemon-web.ps1 -Port {{port}}

# 一键启动局域网 Web 服务（Linux/macOS）
[unix]
daemon-web port="64413":
    bash scripts/daemon-web.sh {{port}}

# ── 检查 & 测试 ─────────────────────────────────────

# Rust workspace 检查
check-rust:
    cargo check --workspace

# 全部静态检查
check: check-rust

# 全部测试
test:
    cargo test --workspace

# Rust 测试
test-rust:
    cargo test --workspace

# Rust 格式化检查
fmt:
    cargo fmt --all --check

# Rust Clippy
clippy:
    cargo clippy --workspace --all-targets

# ── 工具 ────────────────────────────────────────────

# 产物状态
[windows]
status:
    @Write-Output "=== Rust binaries ==="
    @if (Test-Path 'target/release/qaqh-daemon.exe') { '  ✓ qaqh-daemon.exe' } else { '  ✗ qaqh-daemon.exe' }
    @if (Test-Path 'target/release/qaqh-workspace.exe') { '  ✓ qaqh-workspace.exe' } else { '  ✗ qaqh-workspace.exe' }

# 清理
clean:
    cargo clean

# 从 version.txt 同步版本号到所有后端配置文件
[windows]
sync-version:
    @pwsh -File scripts/sync-version.ps1
